#!/usr/bin/env fish
#
# Simple script to import OSX "citizen" application plists and kickstart the application.
# Makes a lot of assumptions.

set import_dir "$HOME/.config/defaults"

# Override the default location with `DEFAULTS_CONFIG_DIR`.
if test -n "$DEFAULTS_CONFIG_DIR"
    set import_dir $DEFAULTS_CONFIG_DIR
end

if test -d $import_dir
    mkdir -p $import_dir
end

for file in $import_dir/*
    if not test -f $file
        continue
    end

    # com.lwouis.alt-tab-macos.plist -> com.lwouis.alt-tab-macos
    set fileparts (string split . -- (basename $file))
    set -e fileparts[(count $fileparts)]
    set domain (string join . $fileparts)

    printf "%s Importing defaults for domain: %s %s\n" "---" $domain "---"

    if defaults import $domain $file
        printf "Imported defaults for domain: %s from file %s\n" $domain $file
    else
        printf "Failed to import defaults for domain: %s from file %s\n" $domain $file
        continue
    end

    set bundlepath (lsappinfo info -only bundlepath $domain | cut -d '"' -f4)
    set pid (lsappinfo info -only pid $domain | cut -d '=' -f2)

    if kill $pid > /dev/null 2>&1
        printf "Just killed process from domain: %s with PID: %i\n" $domain $pid
        printf "Relaunching bundle path: %s...\n" $bundlepath
        if open -n $bundlepath > /dev/null 2>&1
            printf "Relaunched bundle path: %s!\n" $bundlepath
        else
            printf "Failed to relaunch bundle path: %s\n" $bundlepath
            continue
        end
    else
        printf "Couldn't find or kill any processes belong to domain: %s, tried PID: %i\n" $domain $pid
        continue
    end

    printf "%s Imported defaults for domain: %s %s\n\n" "---" $domain "---"
end
