#!/usr/bin/env fish
#
# Small script for installing SQLite tools
# Req: BSDTar, RipGrep, FZF


# Config:
#
# git_remote - Must support ls-remote
#
# arch - CPU arch to target
#
# os - OS to download for

set git_remote https://github.com/sqlite/sqlite.git
set arch_list "x86" "x64"
set arch (printf '%s\n' {$arch_list} | fzf) 
set os_list "osx" "linux" "windows"
set os (printf '%s\n' {$os_list} | fzf)
set bin_location $HOME/.bin

# https://www.sqlite.org/download.html#:~:text=Build%20Product%20Names%20and%20Info
set git_tag (git -c 'versionsort.suffix=-' ls-remote --tags --sort='v:refname' $git_remote \
    | rg 'version-' \
    | tail -1 \
    | cut -f 2 \
    | cut -d / -f 3 \
    | cut -d '-' -f 2 \
    | string split .)
set padded_components (string pad -c 0 -w 2 $git_tag[2..3])
set version_tag (string join -n '' $git_tag[1] $padded_components 00)

set download_name "sqlite-tools-$os-$arch-$version_tag"
set download_target "$download_name.zip"
set year (date +%Y)
set download_url "https://www.sqlite.org/$year/$download_target"

curl --proto 'https' --tlsv1.2 -sS "$download_url" | bsdtar -xf-
chmod +x $download_name/*
mv $download_name/* $bin_location
rm -rf $download_name
